*****************************************************************
* Models provided by GaN Systems Inc. are not warranted by      *
* GaN Systems Inc. as                                           *
* fully representing all of the specifications and operating    *
* characteristics of the semiconductor product to which the     *
* model relates. The model describe the characteristics of a    *
* typical device.                                               *
* In all cases, the current data sheet information for a given  *
* device is the final design guideline and the only actual      *
* performance specification.                                    *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions, nor are they intended to replace bread-       *
* boarding for final verification. GaN Systems Inc. therefore   *
* does not assume any liability arising from their use.         *
* GaN Systems Inc. reserves the right to change models without  *
* prior notice.                                                 *
*****************************************************************
*Level 3. This level includes the stray inductance in the package and built in self-heating mechanism.
*
*
.subckt G65306LL_L3d4 gatein drainin sourcein source_S TC TJ
*
*
.param rTC=-0.004	gan_res={33.7e-3}	metal_res={5.9e-3}	gtc=3.0	sh_s = 0.056	sh_d = 0.944
.param  cur=1.131	x0_0=10.1	x0_1=1.7 	x0_2=9.0  	thr = 1.7 	itc=0.147	atc=30.83
.param 	sf=3.29 ld=1.4e-9  ls=1.0e-9 lg=2.9e-9  rg=1.1 lss=3.2e-9  rss=0.035
*
*
Rth_1 T11 TJ	{0.0161}
Rth_2 T22 T11	{0.0538}
Rth_3 T33 T22	{0.199}
Rth_4 TC T33	{0.164}
*
*
Cth_1 0 TJ 		{1.43e-4}
Cth_2 0 T11 	{5.73e-4}
Cth_3 0 T22 	{1.90e-3}
Cth_4 0 T33 	{1.47e-2}
*
*
bdtemp 0 TJ I = (if(v(drain,source)>0,
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,source)-thr)))*
+	v(drain,source)/(1 + max(x0_0+x0_1*(v(gate,source)+x0_2),0.2)*v(drain,source)))*
+	v(drainin,sourcein),
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,drain)-thr)))*
+	v(source, drain)/(1 + max(x0_0+x0_1*(v(gate,drain)+x0_2),0.2)*v(source,drain)))*
+	v(sourcein, drainin)))
*
*
VSENSE_d 3_d drain DC 0.0
ERES_d drain3 3_d value={I(VSENSE_d) * sh_d * (metal_res*(1+1*rTc*(V(TJ)-25)) + gan_res*PWR((v(TJ)+273)/298,gtc)) }
ldx drainin drain3 {ld} Rser=0
*
*
VSENSE_s 3_s source DC 0.0
ERES_s source3 3_s value={I(VSENSE_s) * sh_s * (metal_res*(1+1*rTc*(V(TJ)-25)) + gan_res*PWR((v(TJ)+273)/298,gtc)) }
Lsx source3 sourcein {ls}  Rser=0
*
*
LSSx source_S source_S1 {lss} Rser=0
RSSx source_S1 source3 {rss}
Lgx gatein gate1 {lg} Rser=0
rgx gate1 gate {rg}
*
*
*
*
Rcsdconv drain source {4000Meg}
Rcgsconv gate source {4000Meg}
Rcgdconv gate drain {4000Meg}
*
*
bswitch drain2 source2 I = (if(v(drain2,source2)>0,
+   (cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,source2)-thr)))*
+	v(drain2,source2)/(1 + max(x0_0+x0_1*(v(gate,source2)+x0_2),0.2)*v(drain2,source2))),

+   (-cur*(-(v(TJ)-25)*itc+atc)*log(1.0+exp(26*(v(gate,drain2)-thr)))*
+	v(source2, drain2)/(1 + max(x0_0+x0_1*(v(gate,drain2)+x0_2),0.2)*v(source2,drain2)))))
*
*
R_drain2 drain2 drain {(1e-4)}
R_source2 source2 source {(1e-4)}
*
*
C_GS	gate source  	{6.8e-11 * sf} 
C_GS1	gate source		Q = (	-2.65e-11*(1-1./(1+exp(0.07*(-v(drain, source)+20))))
+								-0.37e-10*(-0.682+0.66/(1+exp(0.11*(-v(drain, source)+.01))))) * x * sf
*
*
C_GS2	gate source		Q = 3.15e-011*log(1+exp(8*(x-0.9))) * sf
*
*
C_GD	gate drain		{0.3e-012 * sf}
C_GD1	gate drain		Q = 375e-12*log(1+exp(0.07*(x+20))) * sf
*
*
C_SD	source drain	{2e-011 * sf}
C_SD1	source drain	Q =  0.85e-9*log(1+exp(0.085*(x+63))) * sf 
+							+1.45e-9*log(1+exp(0.025*(x+180)))  * sf
+							-2.65e-11*(1-1./(1+exp(0.07*(x+20))) * sf)
*
*
.ends
*$
